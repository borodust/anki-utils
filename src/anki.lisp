(cl:in-package :anki-utils)

;;;
;;; https://www.juliensobczak.com/write/2016/12/26/anki-scripting.html
;;; https://decks.fandom.com/wiki/Anki_APKG_format_documentation
;;;

(declaim (special *deck-table*
                  *model-table*
                  *note-table*
                  *config-table*))

(alexandria:define-constant +collection-schema+
    (alexandria:read-file-into-string
     (asdf:system-relative-pathname :anki-utils "src/schema.sql"))
  :test #'equal)


(alexandria:define-constant +field-separator+ (code-char #x1f)
  :test #'equal)


(defun unix-time ()
  (local-time:timestamp-to-unix (local-time:now)))


(defun alist (&rest args)
  (loop for (name value) on args by #'cddr
        collect (cons name value)))


(defun generate-anki-guid ()
  (format nil "窿祜镳蝈疱狒卑泔祆邈ㄣ镤瀛汨狎ǐ吵蜥钿镯贡┅┅┅ㄤ彐躅犷腴汨邈塍蹴篝蜷铉换骈礤痱镡徕禊忮趑弪麸孱篚蝈沐螋衢孱泔溟铉疳蝮瀛铛礅弪吼狎箦铛礅弪篚怏羼箬岜后栳杯桢ㄢ徕屐后趄轭绛麸镢翦趔篝蜷铉哄钽镤轭乎翩俯俯候徜轼倍┅ㄤ彐礤翳镤牦镱哄钽镤瀛牦镱è簌礅镬ㄥ耢趄蹂┅镳糸镱犰篝蝈犴ㄦ矧磲篝蝈犴Ⅳ蝓澧┅ㄤ彐礤翳镤牦镱哄钽镤瀛牦镱è簌礅镬ㄥ耢ф犰箦┅镳糸镱犰篝蝈犴ㄦ矧磲篝蝈犴㈡犰箦┅ㄤ彐躅溴泔溴牦镱怙镬遽鲠祯濠ㄩ篝蜷铉鲠祯Ⅳ蝓澧趄蹂ф犰箦┅ㄤ彐磲泸鏖翳翦眇矧狎溟蝈泗矧è繇瓠溟颟怙澌怙澌ㄡ戾犷潋獒瑚轸璀珏铙眢繇瓠骈戾啜蹰镳瑚轸璀翦眇矧狎骈戾ê疳翳钺礤繇瓠骈戾戾è繇瓠溟蹰镳哄铙躜瀛溟蝈泗矧疳翳钺礤ㄦ矧磲铋廉溟虔蹰镳侯狒轹瀛钺礤篝蜷铉繇瓠骈戾┅┅躅鏖钿痱雉邈痱镧棱镤蹰镳轰屐弭瀛溟蝈泗矧趄邋繇瓠溟忽犰殇狒ㄣ镱篝犷綮舂┅┅┅ㄤ彐磲泸鏖翳犰轶è蝈篝脲螬犰轶怙澌怙澌啜溴篝蝓泗躜轭绛忾钿é脲离妁犰祜鳝雉桢颦脲螬ㄡ戾犷潋獒横扉篝痨轶犰轶舂棱镤┅ㄤ彐珏铄蜷麸犰轶镡赍泗┅ㄤ彐珏铄蜷骝镯犰轶ㄣ灬篌犰轶舂ㄤ彐珏铄蜷麸篑镡赍泗┅ㄤ彐珏铄蜷骝镯篑ㄣ灬篌屮邈豸矧┅ㄤ彐沆狍殇孱糸骈邃īè殇洪铋翎蜱洪洪铋翩矧ㄥ蝌矧⒑殇黹篌轭纰候遽溴殇镦┅ㄤ彐沆狍钺礤īè钺礤洪铋翎蜱侯犴洪铋翩矧ㄥ蝌矧⒑钺礤黹篌轭纰候遽溴钺礤镦┅ㄤ彐沆狍盹溟骈徕戾īè灬篝盹溟骈邃洪铋翎蜱红狍舡盹溟骈邃洪铋翩矧祜汜飙糸礤侯秣候遽溴灬篝盹溟骈邃糸礤镦┅换换遗痔锨换ㄤ彐沆狍犷腴蝈鲮镧īī换换拖呐换ㄤ彐躅骈钿盹溴ㄩ洎ㄧ弭栳箬殇盹溴飙翎忪濯┅ㄤ彐躅蝈玳篝弪盹溴ㄩ盹溴飑箦翩ㄧ弭栳箬殇盹溴飙翎忪濯盹溴飑ㄤ彐沆狍犷腴盹溴ㄩ溴铘殒殄钺礤洎è溽翎洪铋翎蜱轰狒洪铋翩矧铋飑┅ㄤ彐珏铄蜷箫螋骈屐洵镦ㄡ铍榄盹溴飑ê礤翳镤è翳轶犷腴盹溴飑鏖翳箪雉ㄤ狒岍翳轶戾è殇ㄡ戾犷潋獒横篌镢鲠祯溽翎狐箫螋纥┅ㄣ镱è铛礅弪殇殇è篝蜷铉殇疳蝮瀛轭翦珏殇┅癌┅┅ㄤ彐礤翳镤麸犰轶è翳轶犷腴盹溴飑鏖翳箪雉ㄤ狒岍翳轶溽翎┅ㄤ彐礤翳镤骝镯犰轶è沆狍ㄥ耢п铍榄盹溴飑犰轶舂鏖翳犰轶殇钺礤犰轶磲脲轭篝犷沐п铍榄盹溴洪殇侯犴钺礤轰狒犰轶舂┅换换呐盟孟纹汕换ㄤ彐沆狍犷腴溴汶泔铈殓ㄩ溴铘殒殄钺礤洎è溽翎洪铋翎蜱轰狒洪铋翩矧铋飑┅ㄤ彐躅蝈玳篝弪泔铈殓ㄩ泔铈殓箦翩ㄧ弭栳箬殇泔铈殓翎忪濯泔铈殓┅ㄤ彐躅骈钿泔铈殓ㄩ洎ㄧ弭栳箬殇泔铈殓翎忪濯┅ㄤ彐礤翳镤麸犰轶è翳轶犷腴溴汶泔铈殓┅鏖翳箪雉ㄤ狒岍翳轶ㄩ溽翎溽翎ㄡ扉篝狐殇ㄩ洵镦翳轶狐钺礤钺礤镦翳轶狐蝈痨狴顸趄蹂狐灬痼妩ㄡ扉篝狐戾邈杵衢祗狐黹钌铘狐溴灬簏Ж卑狐戾邈枇泗轱铧狐眭祠癌狐蝈鳇ㄡ扉篝狐疱蚰狴卑狐骢爱暗狐轹炱泗狐磲肾禳扯蛋狐遽箦袋碑狐怩蝙趄蹂狐黹钣疳沐暴狐糸礤螯狐磲葬脲铧栋狐躞铧狐铄鼽ㄡ扉篝狐疱蚰狴舶狐溴灬簏Ж卑狐箦疳蜥翦趄蹂狐轭趔Ж珐狐轭轸獒炱徙麸螯驳鞍狐怩蝙趄蹂狐矧溴螯暴狐盹潼狐狨麸痨狴趄蹂┅┅ㄤ彐礤翳镤骝镯犰轶è沆狍ㄥ耢п铍榄溴汶泔铈殓┅犰轶舂鏖翳犰轶殇钺礤犰轶磲脲轭篝犷沐п铍榄溴汶泔铈殓洪殇侯犴钺礤轰狒犰轶舂┅换换呐盟换ㄤ彐沆狍犷腴溴汶ㄩ溴铘殒殄钺礤盹溟骈徕戾è溴筱蜷痿轱洪铋翎蜱轰弩泸轲糸镱洪铋翩矧ㄥ蝌矧⒑溴筱蜷痿轱黹篌轭纰候遽溴溴筱蜷痿轱瞽镦ㄣ狎洵磲洪铋翩矧磲脲栳箬翎忪濠ㄣ镱骈洪铋翎蜱恒镱骈洪铋翩矧铋候遽溴泔铈殓镦┅ㄤ彐礤翳镤轭轸獒扉瀛轭篝犷沐横骠弪è翳轶犷腴溴汶脲鏖翳箪雉ㄣ镱骈绌翳轶躅戾篌泔铈殓箦翩泔铈殓磲脲轭篝犷沐п铍榄溴汶泔铈殓洪ㄩ洵镦翳轶侯犴钺礤镦翳轶┅┅┅ㄤ彐礤翳镤麸犰轶è翳轶犷腴溴汶┅ㄡ扉篝狐殇ㄩ洵镦翳轶狐钺礤钺礤镦翳轶狐溴筱ㄤ弩泸轲糸镱镦翳轶狐躞铧狐盹潼祜汜飙糸礤呼轫弩翎眇麸躅轼灬篝盹溟骈邃糸礤镦翳轶┅狐泔铈ㄩ洵镦翳轶狐屮翦钿义鳇蛋狐泔祆狃箦潼ф犰箦狐怛秣箦蛎镬灬痼邃趄蹂狐铄髟镤狴Ж癌狐糸礤燥溽Ж癌狐澌铧狐屮翦钿五鼽卑狐蝈鲈镤狴Ж癌狐祢钤镤狴Ж癌┅ㄤ彐礤翳镤骝镯犰轶è沆狍ㄥ耢п铍榄溴汶┅犰轶舂鏖翳犰轶殇钺礤溴筱盹潼犰轶磲脲轭篝犷沐п铍榄溴汶洪殇侯犴钺礤红狍舡盹溟骈邃祜汜飙糸礤乎铋麸糸礤篝犴矧盹潼癌轰弩泸轲糸镱溴筱恒镱骈ㄦ轭洵泔铈殓殇┅┅ㄤ彐躅蝈玳篝弪溴汶ㄩ溴汶箦翩ㄧ弭栳箬殇溴汶翎忪濯溴汶┅ㄤ彐躅骈钿溴汶ㄩ洎ㄧ弭栳箬殇溴汶翎忪濯┅ㄤ彐躅蝈玳篝弪汜蜾ㄤ邈汜蜾殇汜蜾鏖翳箪雉ㄣ狎洵磲皓溴汶箦翩ㄧ弭栳箬汜蜾殇汜蜾磲皓汜蜾┅ㄤ彐磲泸滹汜蜾è汜蜾溴汶怙澌怙澌啜鏖翳箪雉ㄣ狎洵磲皓溴汶祜镳骘汜蜾忮轭翳栳箬鲠祯镦汜蜾磲滹痱镧棱镤┅┅换换蜗耘换ㄤ彐沆狍犷腴铒翦ㄩ溴铘殒殄盹溟骈徕戾è珲殇洪铋翎蜱虹蹰洪铋翩矧ㄧ孱弪狒瀛犷腴珲殇候遽溴珲殇镦盹溴洪铋翎蜱喉镤屐洪铋翩矧ㄥ蝌矧⒑盹溴黹篌轭纰候遽溴盹溴飙镦翎珞洪铋翎蜱呼徵洪铋翩矧铋候遽溴翎珞镦ㄦ殄熹洪铋翎蜱烘殄熹洪铋翩矧铋候遽溴骈屐潴镦┅ㄤ彐躅蝈玳篝弪铒翦ㄩ铒翦箦翩ㄧ弭栳箬殇铒翦翎忪濯铒翦┅ㄤ彐躅骈钿铒翦ㄩ洎ㄧ弭栳箬殇铒翦翎忪濯┅ㄤ彐躅泔瘗犷腴铒翦箫躜沐脲殇翎珞铋翎珞痱秭殇邃皓磲脲轭篝犷沐п铍榄铒翦洪矧殇ㄩ洵镦箫躜沐┅红狍舡盹溟骈邃灬篝盹溟骈邃糸礤镦箫躜沐喉镤屐盹溴飙镦箫躜沐呼徵ㄩ翎珞痱秭殇邃翎珞翎珞镦箫躜沐┅烘殄熹ㄦ殄熹蟓镦箫躜沐┅ㄤ彐礤翳镤麸篑è翳轶犷腴铒翦┅戾è箫螋骈屐洵鲠祯铘箫螋骈屐洵镦盹溴飙镦翳轶┅ㄦ殄熹蟓镦翳轶┅┅扉篝扉篝⑸斡乓晌韵铒翦ㄩ洮珲殇黹洮盹洮躞瞵翎珞骒潴箧熹泱蹴骒徵蟋溽翎至陶庞楷楷楷楷楷楷楷楷楷楷咯ㄩ洵镦翳轶ㄧ蹰洵镦翳轶ㄩ洵镦盹溴飙镦翳轶┅祜汜飙糸礤呼轫弩翎眇麸躅轼灬篝盹溟骈邃糸礤镦翳轶┅ㄩ翎珞镦翳轶ㄦ矧磲铋" (tags-of this)) "")
           (format nil (format nil "~{~A~^A~}" (code-char #x1f)) (fields-of this))
           sort-field-value
           (anki-checksum sort-field-value)
           0 ""))))


(defmethod from-sql ((class (eql 'anki-note)) executor)
  (let ((values (funcall executor "SELECT
id,guid,mid,mod,tags,flds
FROM notes")))
    (loop for (id guid mid mod tags flds) in values
          do (register-note id
                            (make-instance 'anki-note
                                           :id id
                                           :guid guid
                                           :model (find-model mid)
                                           :tags (remove-if #'alexandria:emptyp
                                                            (split-sequence:split-sequence #\Space tags))
                                           :fields (split-sequence:split-sequence +field-separator+
                                                                                  flds)
                                           :last-modified (local-time:unix-to-timestamp (or mod 0)))))))

;;;
;;; CARD
;;;
(defclass anki-card (identified modifiable)
  ((note :initarg :note :initform (error ":note missing") :reader note-of)
   (deck :initarg :deck :initform (error ":deck missing") :reader deck-of)
   (ordinal :initarg :ordinal :initform 0 :reader ordinal-of)))


(defmethod to-sql ((this anki-card))
  (list
   (list "INSERT INTO cards (id,nid,did,ord,mod,usn,type,queue,due,ivl,factor,reps,lapses,left,odue,odid,flags,data)
VALUES (?,?,?,?,?,?,?,?,?,?,?,?,?,?,?,?,?,?)"
         (id-of this)
         (id-of (note-of this))
         (id-of (deck-of this))
         (ordinal-of this)
         (local-time:timestamp-to-unix (last-modified-time-of this))
         0 0 0 0 0 0 0 0 0 0 0 0 "")))



(defmethod from-sql ((class (eql 'anki-card)) executor)
  (let ((values (funcall executor "SELECT
id,nid,did,mod,ord
FROM cards")))
    (loop for (id nid did mod ord) in values
          do (alexandria:if-let ((deck (find-deck did)))
               (register-card deck id
                              (make-instance 'anki-card
                                             :id id
                                             :note (find-note nid)
                                             :deck deck
                                             :ordinal ord
                                             :last-modified (local-time:unix-to-timestamp (or mod 0))))
               (warn "Deck A not found for card A. Ignoring" did id)))))

;;;
;;; COLLECTION
;;;
(defclass anki-collection (identified modifiable)
  ((decks :initarg :decks :initform nil :reader decks-of)
   (models :initarg :models :initform nil :reader models-of)
   (created-at :initarg :created :initform (local-time:now) :reader created-time-of)))


(defun encode-default-collection-config ()
  (json:encode-json-to-string
   (alist :|nextPos| 1
          :|estTimes| 'true
          :|activeDecks| '(1)
          :|sortType| "noteFld"
          :|timeLim| 0
          :|sortBackwards| 'false
          :|addToCur| 'true
          :|curDeck| 1
          :|newBury| 'true
          :|newSpread| 0
          :|dueCounts| 'true
          :|curModel| "1"
          :|collapseTime| 1200)))


(defmethod to-sql ((this anki-collection))
  (let ((json:*lisp-identifier-name-to-json* (lambda (identifier) (string identifier)))
        (unix-time-now (unix-time))
        (model-table (make-hash-table :test 'equal))
        (config-table (make-hash-table :test 'equal))
        (deck-table (make-hash-table :test 'equal))
        (card-table (make-hash-table :test 'equal))
        (note-table (make-hash-table :test 'equal))
        sql)
    (loop for deck in (decks-of this)
          do (setf (gethash (id-of deck) deck-table) (to-alist deck)
                   (gethash (id-of deck) config-table) (to-alist (config-of deck)))
             (docards (card deck)
               (let* ((note (note-of card))
                      (model (model-of note)))
                 (setf (gethash (id-of card) card-table) card
                       (gethash (id-of note) note-table) note)
                 (unless (gethash (id-of model) model-table)
                   (setf (gethash (id-of model) model-table) (to-alist model))))))
    (loop for note being the hash-value of note-table
          do (setf sql (nconc (to-sql note) sql)))
    (loop for card being the hash-value of card-table
          do (setf sql (nconc (to-sql card) sql)))
    (list*
     (list "INSERT INTO col (id,crt,mod,scm,ver,dty,usn,ls,conf,models,decks,dconf,tags)
VALUES (?,?,?,?,?,?,?,?,?,?,?,?,?)"
           1
           unix-time-now unix-time-now unix-time-now 11
           0 0 0
           (encode-default-collection-config)
           (json:encode-json-to-string model-table)
           (json:encode-json-to-string deck-table)
           (json:encode-json-to-string config-table)
           "{}")
     sql)))


(defmethod from-sql ((class (eql 'anki-collection)) executor)
  (let ((json:*json-identifier-name-to-lisp* #'identity)
        (json:*boolean-handler* #'decode-json-boolean)
        (*model-table* (make-hash-table))
        (*deck-table* (make-hash-table))
        (*note-table* (make-hash-table))
        (*config-table* (make-hash-table))
        (values (funcall executor "SELECT
id,crt,mod,scm,ver,dty,usn,ls,conf,models,decks,dconf,tags
FROM col")))
    (labels ((parse-id (id)
               (parse-integer (symbol-name id)))
             (parse-encoded-object-map (class map registerer)
               (loop for (encoded-id . object) in (json:decode-json-from-string map)
                     for id = (parse-id encoded-id)
                     for parsed = (from-alist class object)
                     do (funcall registerer id parsed)
                     collect parsed into objects
                     finally (return objects)))
             (parse-models (models)
               (parse-encoded-object-map 'anki-model models #'register-model))
             (parse-configs (models)
               (parse-encoded-object-map 'anki-deck-config models #'register-config))
             (parse-decks (decks)
               (parse-encoded-object-map 'anki-deck decks #'register-deck)))
      (prog1
          (loop for (id crt mod scm ver dty usn ls conf models decks dconf tags) in values
                collect (progn
                          (parse-configs dconf)
                          (make-instance 'anki-collection
                                         :id id
                                         :decks (parse-decks decks)
                                         :models (parse-models models)
                                         :created (local-time:unix-to-timestamp (or crt 0))
                                         :last-modified (local-time:unix-to-timestamp (or mod 0)))))
        (from-sql 'anki-note executor)
        (from-sql 'anki-card executor)))))


(defun save-anki-collections (collections path)
  (with-temporary-directory (tmp-dir)
    (let* ((database-path (merge-pathnames "collection.anki2" tmp-dir))
           (media-path (merge-pathnames "media" tmp-dir)))
      (ensure-directories-exist tmp-dir)
      (sqlite:with-open-database (db database-path)
        (sqlite:execute-multiple-non-query db +collection-schema+)
        (loop for collection in collections
              do (loop for (sql . params) in (to-sql collection)
                       do (apply #'sqlite:execute-non-query db sql params))))
      (alexandria:write-string-into-file "{}" media-path)
      (zip:zip path tmp-dir))))



(defun load-anki-collections (path)
  (with-temporary-directory (tmp-dir)
    (zip:unzip path tmp-dir)
    (sqlite:with-open-database (db (merge-pathnames "collection.anki2" tmp-dir))
      (flet ((%executor (query)
               (sqlite:execute-to-list db query)))
        (from-sql 'anki-collection #'%executor)))))


(defun make-anki-collection-from-deck (deck)
  (let ((model-table (make-hash-table))
        (now (local-time:now)))
    (docards (card deck)
      (let ((model (model-of (note-of card))))
        (unless (gethash (id-of model) model-table)
          (setf (gethash (id-of model) model-table) model))))
    (make-instance 'anki-collection
                   :id 1
                   :decks (list deck)
                   :models (loop for model being the hash-value of model-table
                                 collect model)
                   :created now
                   :last-modified now)))

;;;
;;;
;;;
(defun all-notes (collection)
  (loop with note-map = (make-hash-table)
        for col in collection
        do (loop for deck in (decks-of col)
                 do (docards (card deck)
                      (let ((note (note-of card)))
                        (setf (gethash (id-of note) note-map) note))))
        finally (return (loop for value being the hash-value of note-map
                              collect value))))


(defun find-notes-by-tags (collection &rest tags)
  (loop for note in (all-notes collection)
        when (intersection tags (tags-of note) :test #'string=)
          collect note))


(defun make-note-table-by-primary-field (collection)
  (loop with table = (make-hash-table :test 'equal)
        for note in (all-notes collection)
        do (setf (gethash (first (fields-of note)) table) note)
        finally (return table)))
